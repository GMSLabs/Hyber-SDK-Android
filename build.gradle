subprojects { project ->
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
    }
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile project.file('checkstyle.xml')
        source 'src'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    buildscript {
        repositories {
            jcenter()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.2'
            classpath 'com.google.gms:google-services:3.0.0'
            classpath 'io.realm:realm-gradle-plugin:1.2.0'
        }
    }
}

ext {
    minSdkVersion = 16
    targetSdkVersion = 24
    compileSdkVersion = 24
    buildToolsVersion = '24.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

def supportLibraryVersion = '24.2.1'
def retrofitVersion = '2.1.0'
def okhttpVersion = '3.4.1'
def stethoVersion = '1.4.1'

ext.deps = [
        jetbAnnotations    : 'org.jetbrains:annotations:13.0',

        // Android
        playServicesBase   : 'com.google.android.gms:play-services-base:9.6.0',
        firebaseMessaging  : 'com.google.firebase:firebase-messaging:9.6.0',
        supportRecyclerView: "com.android.support:recyclerview-v7:$supportLibraryVersion",

        // Third Party
        rxandroid          : 'io.reactivex:rxandroid:1.2.1',
        rxjava             : 'io.reactivex:rxjava:1.1.6',
        retrofit           : "com.squareup.retrofit2:retrofit:$retrofitVersion",
        retrofitGsonConv   : "com.squareup.retrofit2:converter-gson:$retrofitVersion",
        retrofitRxAdapter  : "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion",
        okhttp             : "com.squareup.okhttp3:okhttp:$okhttpVersion",
        hawk               : 'com.github.orhanobut:hawk:1.23',
        javaDiffUtils      : 'com.googlecode.java-diff-utils:diffutils:1.3.0',

        // Test dependencies
        junit              : 'junit:junit:4.12',
        stetho             : "com.facebook.stetho:stetho:$stethoVersion",
        stethoOkHttp       : "com.facebook.stetho:stetho-okhttp3:$stethoVersion",
        stethoRealm        : 'com.uphyca:stetho_realm:0.9.0'

]
