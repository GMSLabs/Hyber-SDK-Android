subprojects { project ->
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
    }
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile project.file('checkstyle.xml')
        source 'src'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    buildscript {
        repositories {
            jcenter()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.2.0'
            classpath 'com.google.gms:google-services:3.0.0'
            classpath 'io.realm:realm-gradle-plugin:1.2.0'
        }
    }
}

ext {
    minSdkVersion = 16
    targetSdkVersion = 24
    compileSdkVersion = 24
    buildToolsVersion = '24.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

def androidToolsVersion = '25.1.2'
def supportLibraryVersion = '24.2.1'
def butterknifeLatestReleaseVersion = '8.4.0'

ext.deps = [
        jetbAnnotations    : 'org.jetbrains:annotations:13.0',

        // Android
        android            : 'com.google.android:android:4.1.1.4',
        supportCompat      : "com.android.support:support-compat:$supportLibraryVersion",
        supportAnnotations : "com.android.support:support-annotations:$supportLibraryVersion",
        supportTestRunner  : 'com.android.support.test:runner:0.5',
        androidGradle      : 'com.android.tools.build:gradle:2.1.3',
        lint               : "com.android.tools.lint:lint:$androidToolsVersion",
        lintApi            : "com.android.tools.lint:lint-api:$androidToolsVersion",
        lintChecks         : "com.android.tools.lint:lint-checks:$androidToolsVersion",
        lintTests          : "com.android.tools.lint:lint-tests:$androidToolsVersion",

        // Square
        javapoet           : 'com.squareup:javapoet:1.7.0',

        // Third Party
        javaparser         : 'com.github.javaparser:javaparser-core:2.4.0',

        // Test dependencies
        junit              : 'junit:junit:4.12',
        truth              : 'com.google.truth:truth:0.28',
        robolectric        : 'org.robolectric:robolectric:3.1.2',
        openglApi          : 'org.khronos:opengl-api:gl1.1-android-2.1_r1',
        compiletesting     : 'com.google.testing.compile:compile-testing:0.9',
        autoservice        : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon         : 'com.google.auto:auto-common:0.6',


        butterknife        : "com.jakewharton:butterknife:$butterknifeLatestReleaseVersion",
        butterknifeCompiler: "com.jakewharton:butterknife-compiler:$butterknifeLatestReleaseVersion"
]
